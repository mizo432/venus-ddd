buildscript {
    ext {
        //Library versions
        assertJVersion = '3.6.2'
        logbackVersion = '1.1.11'
        slf4jVersion = '1.7.25'
        findbugsAnnotationsVersion = '3.0.1'
        venusUtilsVersion = '1.0.5'
        guavaVersion = '25.0-jre'
        defaultEncoding = 'UTF-8'
        junitJupiterApiVersion = '5.6.0'
        junitPlatformLauncherVersion = '1.6.0'
    }
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        maven { url 'http://kamatama41.github.com/maven-repository/repository' }
        mavenCentral()
        jcenter()

    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.13"
        classpath("com.github.kamatama41:gradle-git-release-plugin:0.2.0")
        classpath "com.github.spotbugs:spotbugs-gradle-plugin:3.0.0"
        classpath group: 'com.google.guava', name: 'guava', version: '28.1-jre'
    }
}

apply plugin: 'base'
apply plugin: 'idea'
apply plugin: "com.github.kamatama41.git-release"
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: "com.github.spotbugs"
apply plugin: "maven"
apply plugin: 'pmd'

repositories {
    jcenter()
}

group = 'org.venuspj'
idea.module.outputDir = compileJava.destinationDir
idea.module.inheritOutputDirs = true

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

tasks.withType(JavaCompile) {
    options.incremental = true
}

repositories {
    maven {
        url 'http://mizo432.github.com/maven-repository/repository'
    }
}

dependencies {
    compile group: 'org.venuspj', name: 'venus-utils', version: "${venusUtilsVersion}"
    compileOnly group: 'com.google.code.findbugs', name: 'annotations', version: "${findbugsAnnotationsVersion}"
    testCompile group: 'org.assertj', name: 'assertj-core', version: "${assertJVersion}"
    compile group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"
    testRuntime group: 'ch.qos.logback', name: 'logback-core', version: "${logbackVersion}"
    testRuntime group: 'ch.qos.logback', name: 'logback-classic', version: "${logbackVersion}"
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitJupiterApiVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitJupiterApiVersion}")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher:${junitPlatformLauncherVersion}")
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.7.1'
}

spotbugs {
    // 失敗しても後続の処理を継続させる
    ignoreFailures = true
    sourceSets = [sourceSets.main]
    toolVersion = "4.0.0-RC1"
}

pmd {
    // 失敗しても後続の処理を継続させる
    ignoreFailures = true
    // incrementalAnalysis = true
    sourceSets = [sourceSets.main]
}

tasks.withType(Pmd) {
    reports {
        xml.enabled = true
    }
}

// CPD（重複コードチェック処理）をCheckタスクに追加
check.doLast {
    File outputDir = new File("$reportsDir/cpd/")
    outputDir.mkdirs()

    ant.taskdef(
            name: 'cpd',
            classname: 'net.sourceforge.pmd.cpd.CPDTask',
            classpath: configurations.pmd.asPath)

    ant.cpd(
            minimumTokenCount: '100',
            format: 'xml',
            encoding: defaultEncoding,
            outputFile: new File(outputDir, 'cpd.xml')
    ) {
        fileset(dir: "src/main/java") {
            include(name: '**/*.java')
            exclude(name: '**/*Mock.java')
        }
    }
}

test {
    // 失敗しても後続の処理を継続させる
    ignoreFailures = true
    useJUnitPlatform {
        includeTags 'SMALL'
        excludeTags('MEDIUM', 'LARGE')
    }
    reports {
        junitXml.enabled = true
    }
}

task testSlow(type: Test) {
    ignoreFailures = true
    useJUnitPlatform {
        includeTags('SMALL','MEDIUM','LARGE')
        excludeTags()
    }
    reports {
        junitXml.enabled = true
    }
}

jacoco {
    toolVersion = '0.8.2'
    reportsDir = file("$buildDir/customJacocoReportDir")

}

jacocoTestReport {
    reports {
        xml.enabled = true
    }

}

gitRelease {
    groupId = "org.venuspj"
    artifactId = "venus-ddd"
    repoUri = "https://github.com/mizo432/maven-repository.git"
}